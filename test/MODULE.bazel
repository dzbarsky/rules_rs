module(name = "rules_rs_test")

bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.5.6")

osx = use_extension("@toolchains_llvm_bootstrapped//extensions:osx.bzl", "osx")
osx.framework(name = "CoreFoundation")
osx.framework(name = "CoreServices")
osx.framework(name = "CFNetwork")
osx.framework(name = "DiskArbitration")
osx.framework(name = "AudioUnit")
osx.framework(name = "AudioToolbox")
osx.framework(name = "CoreAudio")
osx.framework(name = "CoreAudioTypes")
osx.framework(name = "OpenAL")
osx.framework(name = "CoreMIDI")
osx.framework(name = "IOKit")
osx.framework(name = "Security")
use_repo(osx, "macosx15.4.sdk")

#local_path_override(
#    module_name = "toolchains_llvm_bootstrapped",
#    path = "/Users/dzbarsky/toolchains_llvm_bootstrapped",
#)

register_toolchains("@toolchains_llvm_bootstrapped//toolchain:all")

bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "bazel_lib", version = "3.2.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "rules_rust", version = "0.68.1")
bazel_dep(name = "nasm", version = "2.16.03.bcr.2")
single_version_override(
    module_name = "nasm",
    patch_strip = 1,
    patches = [
        "//:nasm-musl.patch",
        "//:nasm-include.patch",
    ],
)

single_version_override(
    module_name = "libffi",
    patch_strip = 1,
    patches = ["//:libffi-aarch64-trampoline.patch"],
)

single_version_override(
    module_name = "rules_rust",
    patch_strip = 1,
    patches = [
        "//:rules_rust.patch",
        "//:rules_rust_windows_gnu.patch",
    ],
)

#local_path_override(
#    module_name = "rules_rust",
#    path = "../../rules_rust",
#)

RUST_TRIPLES = [
    "aarch64-unknown-linux-musl",
    "aarch64-apple-darwin",
    "aarch64-pc-windows-gnullvm",
    "x86_64-unknown-linux-musl",
    "x86_64-apple-darwin",
    "x86_64-pc-windows-gnullvm",
]

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = RUST_TRIPLES,
    versions = ["1.89.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

bazel_dep(name = "rules_rs", version = "0.0.0")
local_path_override(
    module_name = "rules_rs",
    path = "..",
)

crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")

TESTS = [
    # TODO(zbarsky): Figure out how to make this work?
    # "annotation_crate_features",
    "bindgen",
    "build_script_aliases",
    "build_script_env_select",
    "build_script_runtime_env",
    "build_dep_features",
    "empty_workspace",
    "feature_name_overrides_implicit_dep",
    "git_crates",
    "git_crates_aliased_deps",
    "many_workspace_deps",  # Got count 45796 in 20 rounds
    "multiple_versions_for_dependency",
    "multiple_versions_of_optional_dep",
    "non_optional_dep_feature",
    "optional_build_deps",
    "platform_dep_features",
    "vendored_crate_override",
    "workspace_path_dependency",
    "binaries",
    "uv",
]

[
    crate.from_cargo(
        name = test,
        cargo_lock = "//:%s/Cargo.lock" % test,
        cargo_toml = "//:%s/Cargo.toml" % test,
        # cargo_config = "//:.cargo/config.toml",
        # use_home_cargo_credentials = True,
        #debug = True,
        platform_triples = RUST_TRIPLES,
    )
    for test in TESTS
]

[use_repo(crate, test) for test in TESTS]

crate.annotation(
    crate = "rustls",
    # Test for https://github.com/bazelbuild/rules_rust/issues/3626
    crate_features = ["fips"],
    repositories = ["annotation_crate_features"],
)
crate.annotation(
    # Smoketest that we do something reasonable with this attribute
    build_script_env_select = {
        "aarch64-apple-darwin": '{"OS": "darwin"}',
        "x86_64-unknown-linux-musl": '{"OS": "linux"}',
    },
    crate = "bitflags",
    repositories = ["build_script_env_select"],
)
crate.annotation(
    crate = "tokenizers",
    workspace_cargo_toml = "tokenizers/Cargo.toml",
)

# Test gen_binaries
crate.annotation(
    crate = "protoc-gen-prost",
    gen_binaries = ["protoc-gen-prost"],
)

# Fix readme inclusions
crate.annotation(
    crate = "windows-link",
    patch_args = ["-p1"],
    patches = ["//:windows-link.patch"],
)
crate.annotation(
    crate = "windows-version",
    patch_args = ["-p1"],
    patches = ["//:windows-version.patch"],
)

# Workaround not parsing `members = ["crates/*"]` yet.
[
    crate.annotation(
        crate = member,
        strip_prefix = "crates/" + member,
    )
    for member in [
        "uv",
        "uv-performance-memory-allocator",
    ]
]

### Fix -sys crates
include("//:3rd_party/bzip2-sys/include.MODULE.bazel")
include("//:3rd_party/glib-sys/include.MODULE.bazel")
include("//:3rd_party/lzma-sys/include.MODULE.bazel")
include("//:3rd_party/zstd-sys/include.MODULE.bazel")

# TODO(zbarsky): alsa-sys is not curently tested, fix it
include("//:3rd_party/alsa-sys/include.MODULE.bazel")
include("//:3rd_party/openssl-sys/include.MODULE.bazel")

single_version_override(
    module_name = "openssl",
    patch_strip = 1,
    patches = ["//:openssl-perl-genrule.patch"],
)

crate.annotation(
    crate = "ring",
    patch_args = ["-p1"],
    patch_tool = "patch",
    patches = ["//:ring-aarch64-windows-v6.patch"],
    strip_prefix = "ring-0.16.20",
    version = "0.16.20",
)

bazel_dep(name = "aws-lc", version = "1.67.0")

crate.annotation(
    build_script_data = [
        "@aws-lc//:crypto",
        "@aws-lc//:ssl",
    ],
    build_script_env = {
        "AWS_LC_SYS_PREBUILT_CRYPTO": "$(rootpaths @aws-lc//:crypto)",
        "AWS_LC_SYS_PREBUILT_SSL": "$(rootpaths @aws-lc//:ssl)",
    },
    crate = "aws-lc-sys",
    patch_args = ["-p1"],
    patches = ["//:aws-lc-sys-prebuilt.patch"],
    deps = [
        "@aws-lc//:crypto",
        "@aws-lc//:ssl",
    ],
)

inject_repo(crate, "aws-lc")

include("//:3rd_party/tikv-jemalloc-sys/include.MODULE.bazel")

crate.annotation(
    build_script_env = {
        "NASM": "$(execpath @nasm//:nasm)",
    },
    build_script_tags = ["annotation-build-script-tag-test"],
    build_script_tools = ["@nasm//:nasm"],
    crate = "rav1e",
    patch_tool = "patch",
    patches = ["//:rav1e-nasm-0.8.1.patch"],
    tags = ["annotation-tag-test"],
    version = "0.8.1",
)

inject_repo(crate, "nasm")

crate.annotation(
    crate = "gemm-common",
    patch_args = ["-p1"],
    patches = ["//:gemm-common-fp16.patch"],
)

include("//:3rd_party/gdk-pixbuf-sys/include.MODULE.bazel")
include("//:3rd_party/cairo-sys-rs/include.MODULE.bazel")
include("//:3rd_party/atk-sys/include.MODULE.bazel")

crate.annotation(
    crate = "khronos_api",
    patches = ["//:khronos_api.patch"],
)

bazel_dep(name = "mimalloc", version = "2.2.4.bcr.1")

"""
ERROR: /Users/dzbarsky/Library/Caches/bazel/_bazel_dzbarsky/16a3c4547ef278628dec1184581db8ea/external/mimalloc+/BUILD.bazel:77:18: Linking external/mimalloc+/libmimalloc-override.so failed: (Exit 1): clang++ failed: error executing CppLink command (from cc_shared_library rule target @@mimalloc+//:mimalloc-override) external/toolchains_llvm_bootstrapped++http_archive+llvm-toolchain-minimal-21.1.8-linux-amd64/bin/clang++ -target aarch64-w64-windows-gnu '--sysroot=/dev/null' '--unwindlib=none' -nostdlib++ ... (remaining 52 arguments skipped)
clang++: error: no such file or directory: 'psapi.lib'
clang++: error: no such file or directory: 'shell32.lib'
clang++: error: no such file or directory: 'user32.lib'
clang++: error: no such file or directory: 'advapi32.lib'
clang++: error: no such file or directory: 'bcrypt.lib'
"""
#crate.annotation(
#    crate = "libmimalloc-sys",
#    gen_build_script = "off",
#    deps = ["@mimalloc"],
#)

inject_repo(crate, "mimalloc")

include("//:3rd_party/libz-sys/include.MODULE.bazel")
include("//:3rd_party/libgit2-sys/include.MODULE.bazel")

crate.annotation(
    additive_build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_import")

cc_import(
    name = "windows_import_lib",
    static_library = glob(["lib/*.a"])[0],
)
""",
    crate = "windows_x86_64_gnullvm",
    gen_build_script = "off",
    deps = [":windows_import_lib"],
)
crate.annotation(
    additive_build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_import")

cc_import(
    name = "windows_import_lib",
    static_library = glob(["lib/*.a"])[0],
)
""",
    crate = "windows_aarch64_gnullvm",
    gen_build_script = "off",
    deps = [":windows_import_lib"],
)

include("//:3rd_party/coreaudio-sys/include.MODULE.bazel")

# TODO(zbarsky): fix this for realz
crate.annotation(
    crate = "pango-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gdk-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gdkx11-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gtk-sys",
    gen_build_script = "off",
)
