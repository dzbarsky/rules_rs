bazel_dep(name = "platforms", version = "1.0.0")
bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.3.1")
archive_override(
    module_name = "toolchains_llvm_bootstrapped",
    integrity = "sha256-es2Zf7dKYEVDuvkm6E90CnunXSisameDh5/5nyhyu2Q=",
    strip_prefix = "toolchains_llvm_bootstrapped-873b8be7d416283761b158ef4635e605970ab3db",
    urls = ["https://github.com/cerisier/toolchains_llvm_bootstrapped/archive/873b8be7d416283761b158ef4635e605970ab3db.tar.gz"],
)

osx = use_extension("@toolchains_llvm_bootstrapped//toolchain/extension:osx.bzl", "osx")

osx.framework(name = "CoreFoundation")
osx.framework(name = "CoreServices")
osx.framework(name = "CFNetwork")
osx.framework(name = "DiskArbitration")
osx.framework(name = "IOKit")
osx.framework(name = "Security")

#local_path_override(
#    module_name = "toolchains_llvm_bootstrapped",
#    path = "../../toolchains_llvm_bootstrapped",
#)

register_toolchains(
    "@toolchains_llvm_bootstrapped//toolchain:all",
)

bazel_dep(name = "rules_python", version = "1.7.0")
bazel_dep(name = "bazel_lib", version = "3.1.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "rules_rust", version = "0.68.1")
bazel_dep(name = "nasm", version = "2.16.03.bcr.2")
single_version_override(
    module_name = "nasm",
    patch_strip = 1,
    patches = ["//:nasm-musl.patch"],
)

single_version_override(
    module_name = "rules_rust",
    patch_strip = 1,
    patches = [
        "//:rules_rust.patch",
        "//:rules_rust_windows_gnu.patch",
    ],
)

#local_path_override(
#    module_name = "rules_rust",
#    path = "../../rules_rust",
#)

RUST_TRIPLES = [
    "aarch64-unknown-linux-musl",
    "aarch64-apple-darwin",
    "aarch64-pc-windows-gnullvm",
    "x86_64-unknown-linux-musl",
    "x86_64-apple-darwin",
    "x86_64-pc-windows-gnullvm",
]

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = RUST_TRIPLES,
    versions = ["1.89.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

bazel_dep(name = "rules_rs", version = "0.0.0")
local_path_override(
    module_name = "rules_rs",
    path = "..",
)

crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")

TESTS = [
    # TODO(zbarsky): Figure out how to make this work?
    # "annotation_crate_features",
    "build_script_aliases",
    "build_script_env_select",
    "build_script_runtime_env",
    "build_dep_features",
    "empty_workspace",
    "feature_name_overrides_implicit_dep",
    "git_crates",
    "many_workspace_deps",  # Got count 45796 in 20 rounds
    "multiple_versions_for_dependency",
    "multiple_versions_of_optional_dep",
    "non_optional_dep_feature",
    "optional_build_deps",
    "platform_dep_features",
    "binaries",
    "uv",
]

[
    crate.from_cargo(
        name = test,
        cargo_lock = "//:%s/Cargo.lock" % test,
        cargo_toml = "//:%s/Cargo.toml" % test,
        # cargo_config = "//:.cargo/config.toml",
        # use_home_cargo_credentials = True,
        #debug = True,
        platform_triples = RUST_TRIPLES,
    )
    for test in TESTS
]

[use_repo(crate, test) for test in TESTS]

crate.annotation(
    crate = "rustls",
    # Test for https://github.com/bazelbuild/rules_rust/issues/3626
    crate_features = ["fips"],
    repositories = ["annotation_crate_features"],
)
crate.annotation(
    # Smoketest that we do something reasonable with this attribute
    build_script_env_select = {
        "aarch64-apple-darwin": '{"OS": "darwin"}',
        "x86_64-unknown-linux-musl": '{"OS": "linux"}',
    },
    crate = "bitflags",
    repositories = ["build_script_env_select"],
)
crate.annotation(
    crate = "tokenizers",
    workspace_cargo_toml = "tokenizers/Cargo.toml",
)

# Test gen_binaries
crate.annotation(
    crate = "protoc-gen-prost",
    gen_binaries = ["protoc-gen-prost"],
)

# Fix readme inclusions
crate.annotation(
    crate = "windows-link",
    patch_args = ["-p1"],
    patches = ["//:windows-link.patch"],
)
crate.annotation(
    crate = "windows-version",
    patch_args = ["-p1"],
    patches = ["//:windows-version.patch"],
)

# Workaround not parsing `members = ["crates/*"]` yet.
[
    crate.annotation(
        crate = member,
        strip_prefix = "crates/" + member,
    )
    for member in [
        "uv",
        "uv-performance-memory-allocator",
    ]
]

### Fix -sys crates
bazel_dep(name = "bzip2", version = "1.0.8.bcr.3")

crate.annotation(
    crate = "bzip2-sys",
    gen_build_script = "off",
    deps = ["@bzip2//:bz2"],
)

inject_repo(crate, "bzip2")

bazel_dep(name = "glib", version = "2.82.2.bcr.7")

crate.annotation(
    crate = "gio-sys",
    gen_build_script = "off",
    deps = ["@glib//gio"],
)
crate.annotation(
    crate = "glib-sys",
    gen_build_script = "off",
    deps = ["@glib//glib"],
)
crate.annotation(
    crate = "gobject-sys",
    gen_build_script = "off",
    deps = ["@glib//gobject"],
)

inject_repo(crate, "glib")

bazel_dep(name = "xz", version = "5.4.5.bcr.7")

crate.annotation(
    crate = "lzma-sys",
    # TODO(zbarsky): Doesn't work on windows arm?
    #gen_build_script = "off",
    #deps = ["@xz//:lzma"],
    gen_build_script = "on",
)

inject_repo(crate, "xz")

bazel_dep(name = "zstd", version = "1.5.7")

crate.annotation(
    crate = "zstd-sys",
    gen_build_script = "off",
    deps = ["@zstd"],
)

inject_repo(crate, "zstd")

# TODO(zbarsky): alsa-sys is not curently tested, fix it
bazel_dep(name = "alsa_lib", version = "1.2.9.bcr.4")

crate.annotation(
    crate = "alsa-sys",
    gen_build_script = "off",
    deps = ["@alsa_lib"],
)

inject_repo(crate, "alsa_lib")

bazel_dep(name = "openssl", version = "3.5.4.bcr.0")
single_version_override(
    module_name = "openssl",
    patch_strip = 1,
    patches = ["//:openssl-perl-genrule.patch"],
    version = "3.5.4.bcr.0",
)

crate.annotation(
    build_script_data = [
        "@openssl//:gen_dir",
    ],
    build_script_env = {
        "OPENSSL_DIR": "$(execpath @openssl//:gen_dir)",
        "OPENSSL_NO_VENDOR": "1",
        "OPENSSL_STATIC": "1",
    },
    crate = "openssl-sys",
    data = ["@openssl//:gen_dir"],
)

inject_repo(crate, "openssl")

crate.annotation(
    crate = "ring",
    patch_args = ["-p1"],
    patch_tool = "patch",
    patches = ["//:ring-aarch64-windows-v6.patch"],
    strip_prefix = "ring-0.16.20",
    version = "0.16.20",
)

bazel_dep(name = "aws-lc", version = "5.3.0-bcr.alpha.4")
local_path_override(
    module_name = "aws-lc",
    path = "../../aws-lc",
)

crate.annotation(
    build_script_data = [
        "@aws-lc//:crypto",
        "@aws-lc//:ssl",
    ],
    build_script_env = {
        "AWS_LC_SYS_PREBUILT_CRYPTO": "$(rootpaths @aws-lc//:crypto)",
        "AWS_LC_SYS_PREBUILT_SSL": "$(rootpaths @aws-lc//:ssl)",
    },
    crate = "aws-lc-sys",
    patch_args = ["-p1"],
    patches = ["//:aws-lc-sys-prebuilt.patch"],
    deps = [
        "@aws-lc//:crypto",
        "@aws-lc//:ssl",
    ],
)

inject_repo(crate, "aws-lc")

bazel_dep(name = "jemalloc", version = "5.3.0-bcr.alpha.4")

crate.annotation(
    crate = "tikv-jemalloc-sys",
    gen_build_script = "off",
    deps = ["@jemalloc"],
)

inject_repo(crate, "jemalloc")

crate.annotation(
    build_script_env = {
        "NASM": "$(execpath @nasm//:nasm)",
    },
    build_script_tools = ["@nasm//:nasm"],
    crate = "rav1e",
    patch_args = ["-p1"],
    patch_tool = "patch",
    patches = ["//:rav1e-nasm-0.8.1.patch"],
    version = "0.8.1",
)

inject_repo(crate, "nasm")

crate.annotation(
    crate = "gemm-common",
    patch_args = ["-p1"],
    patches = ["//:gemm-common-fp16.patch"],
)

bazel_dep(name = "gdk-pixbuf", version = "2.44.4")

crate.annotation(
    crate = "gdk-pixbuf-sys",
    gen_build_script = "off",
    deps = ["@gdk-pixbuf"],
)

inject_repo(crate, "gdk-pixbuf")

bazel_dep(name = "cairo", version = "5.3.0-bcr.alpha.4")
local_path_override(
    module_name = "cairo",
    path = "../../cairo",
)

crate.annotation(
    crate = "cairo-sys-rs",
    gen_build_script = "off",
    deps = ["@cairo"],
)

inject_repo(crate, "cairo")

bazel_dep(name = "at-spi2-core", version = "2.58.2")

crate.annotation(
    crate = "atk-sys",
    gen_build_script = "off",
    deps = ["@at-spi2-core//atk"],
)

inject_repo(crate, "at-spi2-core")

crate.annotation(
    crate = "khronos_api",
    patches = ["//:khronos_api.patch"],
)

### Is this correct?
bazel_dep(name = "mimalloc", version = "2.2.4")

#crate.annotation(
#    crate = "libmimalloc-sys",
#    gen_build_script = "off",
#    deps = ["@mimalloc"],
#)

inject_repo(crate, "mimalloc")

crate.annotation(
    additive_build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_import")

cc_import(
    name = "windows_import_lib",
    static_library = glob(["lib/*.a"])[0],
)
""",
    crate = "windows_x86_64_gnullvm",
    gen_build_script = "off",
    deps = [":windows_import_lib"],
)
crate.annotation(
    additive_build_file_content = """
load("@rules_cc//cc:defs.bzl", "cc_import")

cc_import(
    name = "windows_import_lib",
    static_library = glob(["lib/*.a"])[0],
)
""",
    crate = "windows_aarch64_gnullvm",
    gen_build_script = "off",
    deps = [":windows_import_lib"],
)

# TODO(zbarsky): fix this for realz
crate.annotation(
    crate = "pango-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gdk-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gdkx11-sys",
    gen_build_script = "off",
)
crate.annotation(
    crate = "gtk-sys",
    gen_build_script = "off",
)
