bazel_dep(name = "platforms", version = "1.0.0")

bazel_dep(name = "toolchains_llvm_bootstrapped", version = "0.3.1")
archive_override(
    module_name = "toolchains_llvm_bootstrapped",
    #patch_strip = 1,
    #patches = ["//:toolchains_llvm_bootstrapped.patch"],
    integrity = "sha256-yRDgDshs4PesvkmzSCwNutJjObeF00870DNoJ+WtJhQ=",
    strip_prefix = "toolchains_llvm_bootstrapped-120bb9689efe19cedadec5deb7c45508ad8df4d4",
    urls = ["https://github.com/cerisier/toolchains_llvm_bootstrapped/archive/120bb9689efe19cedadec5deb7c45508ad8df4d4/master.tar.gz"],
)

#local_path_override(
#    module_name = "toolchains_llvm_bootstrapped",
#    path = "../../toolchains_llvm_bootstrapped",
#)

register_toolchains(
    "@toolchains_llvm_bootstrapped//toolchain:all",
)

bazel_dep(name = "bazel_lib", version = "3.0.0")
bazel_dep(name = "rules_cc", version = "0.2.16")
bazel_dep(name = "rules_rust", version = "0.68.1")
single_version_override(
    module_name = "rules_rust",
    patch_strip = 1,
    patches = [
        "//:rules_rust.patch",
        "//:rules_rust_windows_gnu.patch",
    ],
)

#local_path_override(
#    module_name = "rules_rust",
#    path = "../../rules_rust",
#)

RUST_TRIPLES = [
    "aarch64-unknown-linux-musl",
    "aarch64-apple-darwin",
    "aarch64-pc-windows-gnullvm",
    "x86_64-unknown-linux-musl",
    "x86_64-apple-darwin",
    "x86_64-pc-windows-gnullvm",
]

rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = RUST_TRIPLES,
    versions = ["1.89.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

bazel_dep(name = "rules_rs", version = "0.0.0")
local_path_override(
    module_name = "rules_rs",
    path = "..",
)

crate = use_extension("@rules_rs//rs:extensions.bzl", "crate")

TESTS = [
    # TODO(zbarsky): Figure out how to make this work?
    # "annotation_crate_features",
    "build_script_aliases",
    "build_script_env_select",
    "build_script_runtime_env",
    "build_dep_features",
    "feature_name_overrides_implicit_dep",
    "git_crates",
    "many_workspace_deps", # Got count 45796 in 20 rounds
    "multiple_versions_for_dependency",
    "multiple_versions_of_optional_dep",
    "non_optional_dep_feature",
    "optional_build_deps",
    "platform_dep_features",
    "binaries",
    "uv",
]

[
    crate.from_cargo(
        name = test,
        cargo_lock = "//:%s/Cargo.lock" % test,
        cargo_toml = "//:%s/Cargo.toml" % test,
        # cargo_config = "//:.cargo/config.toml",
        # use_home_cargo_credentials = True,
        #debug = True,
        platform_triples = RUST_TRIPLES,
    )
    for test in TESTS
]

[use_repo(crate, test) for test in TESTS]

crate.annotation(
    crate = "rustls",
    # Test for https://github.com/bazelbuild/rules_rust/issues/3626
    crate_features = ["fips"],
    repositories = ["annotation_crate_features"],
)
crate.annotation(
    # Smoketest that we do something reasonable with this attribute
    build_script_env_select = {
        "aarch64-apple-darwin": '{"OS": "darwin"}',
        "x86_64-unknown-linux-musl": '{"OS": "linux"}',
    },
    crate = "bitflags",
    repositories = ["build_script_env_select"],
)
crate.annotation(
    crate = "tokenizers",
    workspace_cargo_toml = "tokenizers/Cargo.toml",
)

# Test gen_binaries
crate.annotation(
    crate = "protoc-gen-prost",
    gen_binaries = ["protoc-gen-prost"],
)

# Fix readme inclusions
crate.annotation(
    crate = "windows-link",
    patch_args = ["-p1"],
    patches = ["//:windows-link.patch"],
)

crate.annotation(
    crate = "windows-version",
    patch_args = ["-p1"],
    patches = ["//:windows-version.patch"],
)

# Workaround not parsing `members = ["crates/*"]` yet.
[
    crate.annotation(
        crate = member,
        strip_prefix = "crates/" + member,
    )
    for member in [
        "uv",
        "uv-performance-memory-allocator",
    ]
]


### Fix -sys crates
bazel_dep(name = "glib", version = "2.82.2.bcr.6")
crate.annotation(
    crate = "glib-sys",
    gen_build_script = "off",
    deps = ["@glib//glib"],
)
inject_repo(crate, "glib")

bazel_dep(name = "alsa_lib", version = "1.2.9.bcr.4")
crate.annotation(
    crate = "alsa-sys",
    gen_build_script = "off",
    deps = ["@alsa_lib"],
)
inject_repo(crate, "alsa_lib")

bazel_dep(name = "openssl", version = "3.5.4.bcr.0")
crate.annotation(
    crate = "openssl-sys",
    build_script_data = [
        "@openssl//:gen_dir",
    ],
    build_script_env = {
        "OPENSSL_DIR": "$(execpath @openssl//:gen_dir)",
        "OPENSSL_NO_VENDOR": "1",
        "OPENSSL_STATIC": "1",
    },
    data = ["@openssl//:gen_dir"],
)
inject_repo(crate, "openssl")

crate.annotation(
    crate = "aws-lc-sys",
    build_script_env = {
        "AWS_LC_SYS_CFLAGS": "-fuse-ld=lld -Wno-unused-command-line-argument",
    },
)

bazel_dep(name = "jemalloc", version = "5.3.0-bcr.alpha.4")
crate.annotation(
    crate = "tikv-jemalloc-sys",
    gen_build_script = "off",
    deps = ["@jemalloc"],
    #build_script_data = ["@openai//:jemalloc_lib"],
    #build_script_env = {
    #    "JEMALLOC_OVERRIDE": "$(location @openai//:jemalloc_lib)",
    #},
)
inject_repo(crate, "jemalloc")

