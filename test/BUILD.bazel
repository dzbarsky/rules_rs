load("@bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_cc//cc:defs.bzl", "cc_shared_library")
load("@rules_rust//rust:defs.bzl", "rust_binary")

cc_shared_library(
    name = "clang",
    deps = ["@llvm-project//clang:libclang"],
    visibility = ["//visibility:public"],
)

DISABLED = [
    "annotation_crate_features",
    #"binaries",
    #"build_script_aliases",
    #"build_script_env_select",
    #"build_script_runtime_env",
    #"build_dep_features",
    #"feature_name_overrides_implicit_dep",
    #"many_workspace_deps",
    #"multiple_versions_for_dependency",
    #"multiple_versions_of_optional_dep",
    #"non_optional_dep_feature",
    #"optional_build_deps",
    #"platform_dep_features",
    #"git_crates",
    #"uv",
]

TESTS = [f for f in glob(
    ["*"],
    exclude_directories = 0,
) if "." not in f and "bazel-" not in f and f not in DISABLED]

rust_binary(
    name = "rust_binary",
    srcs = ["main.rs"],
    deps = [
        "@many_workspace_deps//:actix-web",
        "@many_workspace_deps//:apple-codesign",
        "@many_workspace_deps//:axum",
        "@many_workspace_deps//:ballista",
        "@many_workspace_deps//:cssparser",
        "@many_workspace_deps//:datafusion",
        "@many_workspace_deps//:deltalake",
        "@many_workspace_deps//:diesel",
        "@many_workspace_deps//:ethers",
        "@many_workspace_deps//:iced",
        "@many_workspace_deps//:image",
        "@many_workspace_deps//:oxc",
        "@many_workspace_deps//:polars",
        "@many_workspace_deps//:reqwest",
        "@many_workspace_deps//:swc",
        "@many_workspace_deps//:tantivy",
        "@many_workspace_deps//:tao",
        "@many_workspace_deps//:tonic",
        "@many_workspace_deps//:tower",
        "@many_workspace_deps//:walrus",
        "@many_workspace_deps//:warp",
        "@many_workspace_deps//:wasmtime",
        "@many_workspace_deps//:wgpu",
        "@many_workspace_deps//:yew",
    ],
)

genquery(
    name = "openssl_sys_has_annotation_tag",
    expression = 'attr("tags", ".*annotation-tag-test.*", deps(@optional_build_deps//:openssl-sys))',
    scope = ["@optional_build_deps//:openssl-sys"],
)

genquery(
    name = "openssl_sys_build_script_has_annotation_tag",
    expression = 'attr("tags", ".*annotation-build-script-tag-test.*", deps(@optional_build_deps//:openssl-sys))',
    scope = ["@optional_build_deps//:openssl-sys"],
)

genrule(
    name = "verify_annotation_tags",
    srcs = [
        ":openssl_sys_has_annotation_tag",
        ":openssl_sys_build_script_has_annotation_tag",
    ],
    outs = ["verify_annotation_tags.txt"],
    cmd = """
        test -s $(location :openssl_sys_has_annotation_tag)
        test -s $(location :openssl_sys_build_script_has_annotation_tag)
        echo ok > $@
    """,
)

filegroup(
    name = "all_builds",
    srcs = ["@%s//:_workspace_deps" % test for test in TESTS] + [
        "@binaries//:protoc-gen-prost__protoc-gen-prost",
        ":rust_binary",
        ":verify_annotation_tags",
    ],
)

ALL_PLATFORMS = [
    "linux_arm64",
    "linux_amd64",
    #"linux_arm64_musl",
    #"linux_amd64_musl",
    "macos_amd64",
    "macos_arm64",
    "windows_amd64",
    "windows_arm64",
]

[
    platform_transition_filegroup(
        name = "all_builds_" + platform,
        target_platform = "@toolchains_llvm_bootstrapped//platforms:" + platform,
        srcs = ["all_builds"],
    )
    for platform in ALL_PLATFORMS
]

platform(
    name = "rbe",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
        "@bazel_tools//tools/cpp:clang",
    ],
    exec_properties = {
        "container-image": "docker://ubuntu:22.04",
        "OSFamily": "Linux",
    },
)
