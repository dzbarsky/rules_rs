load("@bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_rs//rs:rust_binary.bzl", "rust_binary")
load("@rules_shell//shell:sh_test.bzl", "sh_test")

sh_test(
    name = "dummy_test",
    srcs = ["dummy_test.sh"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

DISABLED = [
    "annotation_crate_features",
    "3rd_party",
    #"binaries",
    #"build_script_aliases",
    #"build_script_env_select",
    #"build_script_runtime_env",
    #"build_dep_features",
    #"feature_name_overrides_implicit_dep",
    #"many_workspace_deps",
    #"multiple_versions_for_dependency",
    #"multiple_versions_of_optional_dep",
    #"non_optional_dep_feature",
    #"optional_build_deps",
    #"platform_dep_features",
    #"git_crates",
    #"uv",
]

TESTS = [f for f in glob(
    ["*"],
    exclude_directories = 0,
) if "." not in f and "bazel-" not in f and f not in DISABLED]

rust_binary(
    name = "rust_binary",
    srcs = ["main.rs"],
    deps = [
        "@many_workspace_deps//:actix-web",
        "@many_workspace_deps//:apriltag",
        "@many_workspace_deps//:apple-codesign",
        "@many_workspace_deps//:axum",
        "@many_workspace_deps//:ballista",
        "@many_workspace_deps//:cssparser",
        "@many_workspace_deps//:datafusion",
        "@many_workspace_deps//:deltalake",
        "@many_workspace_deps//:diesel",
        "@many_workspace_deps//:ethers",
        "@many_workspace_deps//:iced",
        "@many_workspace_deps//:image",
        "@many_workspace_deps//:oxc",
        "@many_workspace_deps//:polars",
        "@many_workspace_deps//:reqwest",
        "@many_workspace_deps//:swc",
        "@many_workspace_deps//:tantivy",
        "@many_workspace_deps//:tao",
        "@many_workspace_deps//:tonic",
        "@many_workspace_deps//:tower",
        "@many_workspace_deps//:walrus",
        "@many_workspace_deps//:warp",
        "@many_workspace_deps//:wasmtime",
        "@many_workspace_deps//:wgpu",
        "@many_workspace_deps//:yew",
    ],
)

genquery(
    name = "rav1e_has_annotation_tag",
    expression = 'attr("tags", ".*annotation-tag-test.*", deps(@build_script_runtime_env//:rav1e))',
    scope = ["@build_script_runtime_env//:rav1e"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

genquery(
    name = "rav1e_build_script_has_annotation_tag",
    expression = 'attr("tags", ".*annotation-build-script-tag-test.*", deps(@build_script_runtime_env//:rav1e))',
    scope = ["@build_script_runtime_env//:rav1e"],
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

genquery(
    name = "cfg_feature_target_dep_inner_deps",
    expression = "deps(@cfg_feature_target_dep//:inner-0.1.0)",
    scope = ["@cfg_feature_target_dep//:inner-0.1.0"],
)

genrule(
    name = "verify_cfg_feature_target_dep",
    srcs = [":cfg_feature_target_dep_inner_deps"],
    outs = ["verify_cfg_feature_target_dep.txt"],
    cmd = """
        grep '@@rules_rs++crate+cfg_feature_target_dep//:itoa-1.0.15' $(location :cfg_feature_target_dep_inner_deps)
        echo ok > $@
    """,
)

genquery(
    name = "first_party_feature_propagation_dep_leaf_deps",
    expression = "deps(@first_party_feature_propagation//:dep_leaf-0.1.0)",
    scope = ["@first_party_feature_propagation//:dep_leaf-0.1.0"],
)

genrule(
    name = "verify_first_party_feature_propagation",
    srcs = [":first_party_feature_propagation_dep_leaf_deps"],
    outs = ["verify_first_party_feature_propagation.txt"],
    cmd = """
        grep '@@rules_rs++crate+first_party_feature_propagation//:itoa-1.0.17' $(location :first_party_feature_propagation_dep_leaf_deps)
        grep '@@rules_rs++crate+first_party_feature_propagation//:ryu-1.0.23' $(location :first_party_feature_propagation_dep_leaf_deps)
        echo ok > $@
    """,
)

genrule(
    name = "verify_annotation_tags",
    srcs = [
        ":rav1e_has_annotation_tag",
        ":rav1e_build_script_has_annotation_tag",
    ],
    outs = ["verify_annotation_tags.txt"],
    cmd = """
        test -s $(location :rav1e_has_annotation_tag)
        test -s $(location :rav1e_build_script_has_annotation_tag)
        echo ok > $@
    """,
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

filegroup(
    name = "all_builds",
    srcs = ["@%s//:_workspace_deps" % test for test in TESTS] + [
        ":rust_binary",
        ":verify_cfg_feature_target_dep",
        ":verify_first_party_feature_propagation",
        "@binaries//:protoc-gen-prost__protoc-gen-prost",
    ] + select({
        "@platforms//os:windows": [],
        "//conditions:default": [":verify_annotation_tags"],
    }),
)

ALL_PLATFORMS = [
    "aarch64-unknown-linux-gnu",
    "aarch64-apple-darwin",
    "aarch64-pc-windows-gnullvm",
    "x86_64-unknown-linux-gnu",
    "x86_64-apple-darwin",
    "x86_64-pc-windows-gnullvm",
]

[
    platform_transition_filegroup(
        name = "all_builds_" + platform,
        srcs = ["all_builds"],
        target_platform = "@rules_rs//rs/experimental/platforms:" + platform,
    )
    for platform in ALL_PLATFORMS
]

platform(
    name = "rbe",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
        # Note: We need this so that Rust cfg=exec builds (build script, tools, proc_macros)
        # can properly target the remote.
        "@llvm//constraints/libc:gnu.2.28",
    ],
    exec_properties = {
        "container-image": "docker://ubuntu:22.04",
        "OSFamily": "Linux",
    },
)
